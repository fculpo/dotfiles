# shellcheck disable=all

XDG_CONFIG_HOME="$HOME/.config"
XDG_DATA_HOME="$HOME/.local/share"
XDG_CACHE_HOME="$HOME/.cache"

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

EDITOR="vim"
VISUAL="vim"

zstyle ':zim:zmodule' use 'degit'
ZIM_HOME=${XDG_CACHE_HOME}/zim

# Download zimfw plugin manager if missing.
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
  curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
      https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
fi

# Install missing modules and update ${ZIM_HOME}/init.zsh if missing or outdated.
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE:-${ZDOTDIR:-${HOME}}/.zimrc} ]]; then
  source ${ZIM_HOME}/zimfw.zsh init
fi

# Initialize modules.
source ${ZIM_HOME}/init.zsh

# Set editor default keymap to emacs (`-e`) or vi (`-v`)
bindkey -e

# Prompt for spelling correction of commands.
setopt CORRECT

# Customize spelling correction prompt.
SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

# Remove path separator from WORDCHARS.
WORDCHARS=${WORDCHARS//[\/]}

# Append `../` to your input for each `.` you type after an initial `..`
zstyle ':zim:input' double-dot-expand yes

# Set a custom terminal title format using prompt expansion escape sequences.
# See http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html#Simple-Prompt-Escapes
# If none is provided, the default '%n@%m: %~' is used.
zstyle ':zim:termtitle' format '%1~'

# Disable automatic widget re-binding on each precmd. This can be set when
# zsh-users/zsh-autosuggestions is the last module in your ~/.zimrc.
ZSH_AUTOSUGGEST_MANUAL_REBIND=1

# Customize the style that the suggestions are shown with.
# See https://github.com/zsh-users/zsh-autosuggestions/blob/master/README.md#suggestion-highlight-style
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=242'

# Set what highlighters will be used.
# See https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters.md
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

# Customize the main highlighter styles.
# See https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters/main.md#how-to-tweak-it
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[comment]='fg=242'

zmodload -F zsh/terminfo +p:terminfo

# Bind ^[[A/^[[B manually so up/down works both before and after zle-line-init
for key in '^[[A' '^P' ${terminfo[kcuu1]}; do
  bindkey "$key" history-substring-search-up
done
for key in '^[[B' '^N' ${terminfo[kcud1]}; do
  bindkey "$key" history-substring-search-down
done
for key in 'k'; do
  bindkey -M vicmd "$key" history-substring-search-up
done
for key in 'j'; do
  bindkey -M vicmd "$key" history-substring-search-down
done
unset key

# Enable bash completion compatibility.
# After this, you can use complete -F and other bash completion commands in zsh, 
# which is useful for tools that only provide bash completion scripts (like terraform, kubectl, etc).
autoload -U +X bashcompinit && bashcompinit

HISTFILE="$HOME/.zsh_history"
HISTSIZE=50000
SAVEHIST=50000

unsetopt SHARE_HISTORY
setopt INC_APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_FIND_NO_DUPS      # Skip duplicates when searching (Ctrl+R)
setopt HIST_VERIFY            # Show command before executing from history
setopt HIST_REDUCE_BLANKS     # Remove extra blanks from commands

# Completion behavior
unsetopt MENU_COMPLETE        # Don't cycle through completions with TAB
setopt AUTO_LIST              # List choices on ambiguous completion
setopt COMPLETE_IN_WORD       # Complete from cursor position
setopt AUTO_PARAM_SLASH       # Add trailing slash for directories

# Directory navigation
setopt AUTO_CD                # Type directory name to cd into it
setopt PUSHD_IGNORE_DUPS      # No duplicate entries in directory stack

# Globbing
unsetopt CASE_GLOB            # Case-insensitive globbing
setopt EXTENDED_GLOB          # Advanced globbing (**, ^, ~, etc.)
unsetopt GLOB_DOTS            # If enabled, globs match dotfiles - be careful with rm *
setopt NO_NOMATCH             # Pass through unmatched globs instead of error

# Misc
unsetopt PATH_DIRS            # Don't search PATH for slashed commands (security)
setopt NOTIFY                 # Immediate job notifications
setopt NO_FLOW_CONTROL        # Disable Ctrl+S/Ctrl+Q
setopt NO_LIST_BEEP           # No beep on completion list

zstyle ':completion:*' verbose true
zstyle ':completion:*' menu select=0 search
zstyle ':completion:::::' completer _extensions _complete _approximate

# Complete the alias when _expand_alias is used as a function
zstyle ':completion:*' complete true
zle -C alias-expension complete-word _generic
bindkey '^xa' alias-expension
zstyle ':completion:alias-expension:*' completer _expand_alias

# Autocomplete options for cd instead of directory stack
zstyle ':completion:*' complete-options true
zstyle ':completion:*' file-sort modification

# Use cache for commands using cache
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/.zcompcache"

zstyle ':completion:*:*:*:*:corrections' format '%F{yellow}!- %d (errors: %e) -!%f'
zstyle ':completion:*:*:*:*:descriptions' format '%F{blue}-- %D %d --%f'
zstyle ':completion:*:*:*:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:*:*:*:warnings' format ' %F{red}-- no matches found --%f'
#zstyle ':completion:*:default' list-prompt '%S%M matches%s'

# Group matches and describe.
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '%d'
#zstyle ':completion:*' format ' %F{yellow}-- %d --%f'

zstyle ':completion:*:*:-command-:*:*' group-order aliases builtins functions commands

# See ZSHCOMPWID "completion matching control"
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

zstyle ':completion:*' keep-prefix true

zstyle -e ':completion:*:(ssh|scp|sftp|rsh|rsync):hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'

zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'users' 'expand'
zstyle ':completion:*' squeeze-slashes true

# Don't complete uninteresting users...
zstyle ':completion:*:*:*:users' ignored-patterns \
  adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
  dbus distcache docker dovecot fax ftp games gdm gkrellmd gopher \
  hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
  mailman mailnull mldonkey mysql nagios \
  named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
  operator pcap postfix postgres privoxy pulse pvm quagga radvd \
  rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs '_*'

# Bind edit-command-line to Ctrl+X, then Ctrl+E (standard for editing the command line in $EDITOR)
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

# Load all files from zshrc.d directory
if [ -d "$HOME/.zsh.d" ]; then
    for file in "$HOME/.zsh.d"/*.zsh(N); do
        source "$file"
    done
fi

# Prevent duplicate PATH entries (zsh feature)
typeset -U path
path=("$HOME/go/bin" "$HOME/.local/bin" "$HOME/.atuin/bin" $path)
# export NODE_EXTRA_CA_CERTS=/etc/ssl/cert.pem

# Set GPG_TTY to the current terminal only if running in an interactive terminal (tty).
# This ensures GPG works correctly for pinentry and other interactive operations.
if [[ -t 1 ]]; then
  GPG_TTY=$(tty)
  export GPG_TTY
fi

[ -f "$HOME/.local/bin/env" ] && source "$HOME/.local/bin/env"
[ -x /opt/homebrew/bin/brew ] && eval "$(/opt/homebrew/bin/brew shellenv)"
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
[ -f "$HOME/.config/wezterm/shell-integration.sh" ] && source "$HOME/.config/wezterm/shell-integration.sh"

command -v zoxide &>/dev/null && eval "$(zoxide init zsh)"
command -v mise &>/dev/null && eval "$(mise activate zsh)"
command -v uv &>/dev/null && eval "$(uv generate-shell-completion zsh)"
command -v uvx &>/dev/null && eval "$(uvx --generate-shell-completion zsh)"
#command -v starship &>/dev/null && eval "$(starship init zsh)"

# [ -f "$HOME/.atuin/bin/env" ] && source "$HOME/.atuin/bin/env"
command -v atuin &>/dev/null && eval "$(atuin init zsh --disable-up-arrow --disable-ctrl-r)"

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
